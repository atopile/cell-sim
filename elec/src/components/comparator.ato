from "generics/vdivs.ato" import VDiv
from "generics/interfaces.ato" import Power
from "generics/mosfets.ato" import LowSideSwitch

module Comparator:
    """
    Dual channel comparator with open collector output (yanks down hard on whatever you connect the output to)

    Usage:
    Set threashold as a percentage of power input voltage
    """
    power = new Power
    signal input_channel_1
    signal input_channel_2
    signal output_channel_1
    signal output_channel_2

    channel_1_threashold = 0.5 +/- 5%
    channel_2_threashold = 0.5 +/- 5%

    ic = new LM393ST
    power ~ ic.power

    # Channel 1
    channel_1_vdiv = new VDiv
    power ~ channel_1_vdiv.power
    channel_1_vdiv.out ~ ic.channel_1_non_inverting
    input_channel_1 ~ ic.channel_1_inverting

    channel_1_vdiv.v_out = power.voltage * channel_1_threashold

    channel_1_fet = new LowSideSwitch
    ic.channel_1_output ~ channel_1_fet.control.io
    channel_1_fet.control.gnd ~ power.gnd
    output_channel_1 ~ channel_1_fet.load.n

    # Channel 2
    channel_2_vdiv = new VDiv
    power ~ channel_2_vdiv.power
    channel_2_vdiv.out ~ ic.channel_2_non_inverting
    input_channel_2 ~ ic.channel_2_inverting


    channel_2_fet = new LowSideSwitch
    ic.channel_2_output ~ channel_2_fet.control.io
    channel_2_fet.control.gnd ~ power.gnd
    output_channel_2 ~ channel_2_fet.load.n


component LM393ST:
    # component LM393ST
    footprint = "MINISO-8_L3.0-W3.0-P0.65-LS4.9-BL"
    lcsc_id = "C2688712"
    mpn = "C2688712"
    # pins

    power = new Power
    power.vcc ~ pin 8
    power.gnd ~ pin 4

    channel_1_inverting ~ pin 2
    channel_1_non_inverting ~ pin 3
    channel_1_output ~ pin 1

    channel_2_inverting ~ pin 6
    channel_2_non_inverting ~ pin 5
    channel_2_output ~ pin 7
