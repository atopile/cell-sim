import Power from "interfaces.ato"
import Pair from "interfaces.ato"
import DiffPair from "interfaces.ato"
import Load from "interfaces.ato"
import Resistor from "resistors.ato"
import Diode from "diodes.ato"

component NFET:
    signal gate
    signal drain
    signal source
    designator_prefix = "Q"
    mpn = "generic_mosfet"
    type = "mosfet"
    polarity = "N Channel"

component PFET:
    signal gate
    signal drain
    signal source
    designator_prefix = "Q"
    mpn = "generic_mosfet"
    type = "mosfet"
    polarity = "P Channel"

module HalfBridge:
    power = new Power
    gate_high = new Pair
    gate_low = new Pair
    shunt_output = new DiffPair
    output = new Pair

    # this doesnt work until we have replace with instance.
    # Components
    nfet_high = new NFET
    nfet_low = new NFET
    shunt = new Resistor

    # Connect power
    power.vcc ~ nfet_high.drain
    output.io ~ nfet_high.source
    output.io ~ nfet_low.drain
    power.gnd ~ shunt.2; shunt.1 ~ nfet_low.source

    # Connect the gates
    gate_high.io ~ nfet_high.gate
    gate_low.io ~ nfet_low.gate

    # Connect the gnds
    output.gnd ~ power.gnd
    gate_high.gnd ~ power.gnd
    gate_low.gnd ~ power.gnd

module LowSideSwitch:
    fet = new NFET
    gate_resistor = new Resistor
    pulldown_resistor = new Resistor

    gate_resistor.value = 1kohm +/- 5%
    pulldown_resistor.value = 10kohm +/- 5%

    power_in = new Power
    load = new Load

    control = new Pair

    # connect up circuit
    control.io ~ gate_resistor.p1; gate_resistor.p2 ~ fet.gate
    control.gnd ~ power_in.gnd

    # Pull down resistor on control
    control.io ~ pulldown_resistor.p1; pulldown_resistor.p2 ~ power_in.gnd

    # load and diode between power and fet
    power_in.vcc ~ load.p; load.n ~ fet.drain

    # source to ground
    fet.source ~ power_in.gnd

import NTTFS4C25NTWG from "elec/src/NTTFS4C25NTWG.ato"
module LowSideSwitch30V5A from LowSideSwitch:
    fet -> NTTFS4C25NTWG
